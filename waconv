#!/usr/bin/perl

# waconv - whatsapp conversion tool
# License: MIT (see LICENSE)

use strict;
use warnings;

$|++;

# locale to convert from
my $L = lc shift @ARGV;

print "$0: Converts whatsapp chats from * to 24h&co. Format
Usage: $0 [lang] [chat file]

Supported locale:
	de:	convert from german
	us:	convert from us format (MM/DD/YYYY)
" and exit if $L =~ /^(h|$)/ or not @ARGV;

die "Locale not supported\n"
	unless $L =~ /^de|us/;

# line, time and date regex
my ($tre, $dre, $lre);

if ($L eq "de") {
  $dre = qr{(\d{2}).(\d{2}).(\d{4})};
  $tre = qr{(\d{1,2}):(\d{2}) (vorm|nachm).};
  $lre = qr{^(\d{2}\.\d{2}\.\d{4}), (\d{1,2}:\d{2} (?:vorm|nachm)\.) - ([^:]*): (.*)$};
} elsif ($L eq "us") {
  $dre = qr{(\d{2})/(\d{2})/(\d{4})};
  $tre = qr{(\d{1,2}):(\d{2}) (am|pm)};
  $lre = qr{^(\d{2}/\d{2}/\d{4}), (\d{1,2}:\d{2} (?:am|pm)) - ([^:]*): (.*)$};
}

sub cd { # convert date
  my ($date) = @_;
  if ($date =~ m/$dre/) {
    my ($day, $month, $year) = ($1, $2, $3);
    return "$day/$month/$year";
  }
}

sub cXXtAM { # convert any variation of AM/PM to standard AM/PM
  my $time = shift @_;
  if ($time =~ m/$tre/) {
    my $noon;
    $noon = $3 eq "vorm" ? "am" : "pm" if $L eq "de";
    $noon = $3  if $L eq "us";
    return "$1:$2 $noon";
  } else { return $time; }
}

sub c12t24 { # convert 12h to 24h
  chomp(my $x = shift @_);
  my @t = split(/[: ]/, $x);
  $t[0]= $t[0] + 12 if $t[2] eq "pm" and $t[0] < 12;
  $t[0]= 0 if $t[0] == 12 and $t[2] eq "am";
  return $t[0], $t[1];
}

sub ct { # covert time
  my ($time) = @_;
  if ($time =~ m/$tre/) {
    my ($hour, $min) = c12t24(cXXtAM($time));
    return "$hour:$min";
  }
}

while (<>) {
  chomp;
  next unless (m/$lre/gx);
  my ($date, $time, $user, $message) = (&cd($1), &ct($2), $3, $4);
  print "$date, $time - $user: $message\n";
}
